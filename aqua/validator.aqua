import "node.aqua"
import "meta-contract.aqua"
import "fevm_json_rpc.aqua"

const NODE_SERVICE_ID = "transaction"
const CONTRACT_SERVICE_ID = "8755a3a2-7e91-4e9e-8885-53955e927ebe"
const ERC1155_ABI_URL = "https://ipfs.xfero.io/ipfs/QmSiZUtqkbEZPPFMN9ZihSbPskNxqkjBpAfB2NmwLv11N7?filename=CollaBeatNFT-abi.json"
const ERC721_ABI_URL = "https://ipfs.xfero.io/ipfs/QmWmkDbQ9KA6yLR8C8TuavLwyk2g6TfHyixG5gwTRem3Nw?filename=ERC721_0x3Zero-abi.json"

service MyOp("op"):
  array_length(results: []string) -> i64

service MyOp2("op"):
  array_length(results: []EventLogParamResult) -> i64

service MyOp3("op"):
  array_length(results: []Cron) -> i64

data EvmLogParam:
  to: string
  token_id: string
  data: string

func init():
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    Node.init()

func validateTransaction(hash: string):
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    tx <- Node.get_transaction(hash)

    if tx.transaction.method == "contract":
      Node.bind_meta_contract(hash)
    
    if tx.transaction.method == "metadata":
      metaContract <- Node.get_meta_contract_by_id(tx.transaction.meta_contract_id)
      if metaContract.success:
        metadatasResult <- Node.get_metadatas(tx.transaction.data_key, "")
        MetaContract metaContract.meta.meta_contract_id
        metaContractResult <- MetaContract.on_execute(metaContract.meta, metadatasResult.metadatas, tx.transaction)
        Node.set_metadata(hash, metaContract.meta.meta_contract_id, metaContractResult.result, metaContractResult.metadatas, metaContractResult.error_string)

        if tx.transaction.token_key != "":
          metaContractToken <- Node.get_meta_contract(tx.transaction.token_key)
          if metaContractToken.success:
            metadatasTokenResult <- Node.get_metadatas(tx.transaction.data_key, "")
            MetaContract metaContractToken.meta.meta_contract_id
            metaContractTokenResult <- MetaContract.on_execute(metaContractToken.meta, metadatasTokenResult.metadatas, tx.transaction)
            Node.set_metadata(hash, metaContractToken.meta.meta_contract_id, metaContractTokenResult.result, metaContractTokenResult.metadatas, metaContractTokenResult.error_string)

    if tx.transaction.method == "cron":
      Node.set_cron(hash)

func getPendingTransactions_3600():
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    result <- Node.get_pending_transactions()

    if result.success:
      for tx <- result.transactions:
        validateTransaction(tx.hash)

func publish(request: TransactionRequest) -> FdbResult:
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    -- result <- Node.publish("1", "11", "", "", "0xc20de1a30487ec70fc730866f297f2e2f1e411f7", "0x9239334cca0d0c7e4eb424fa4604f2c3f6d16c34b466f27ddad0a00fd4f11b581fecf9dc82f6e20e14a2977dcf80d1b72dcbb6592b46977f72afb976e34f9d7e1b", msg, "metadata", count, version)
    result <- Node.publish(request)
  <- result

func pending_txs() -> FdbTransactionsResult:
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    result <- Node.get_pending_transactions()
  <- result

func success_txs(f: i64, t: i64) -> FdbTransactionsResult:
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    result <- Node.get_complete_transactions(f, t)
  <- result

func get_txs(query: []TransactionQuery, ordering: []TransactionOrdering, from: u32, to: u32) -> FdbTransactionsResult:
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    result <- Node.get_transactions(query, ordering, from, to)
  <- result

func processCron():
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    results <- Node.get_active_crons()

    evm_logs: *[]EventLogParamResult
    meta_contract_result: *MetaContractResult
    cron_datas: *Cron

    datas2: *EventLogParamResult

    if results.success:  
      cron_length <- MyOp3.array_length(results.crons)

      for result <- results.crons par:
        cron_datas <<- result
        from_block = Node.get_cron_tx_latest_block(result.address, result.chain, result.topic)
        from_block_hex = FevmJsonRpc.decimal_to_hex(from_block)
        
        if result.token_type == "erc1155":
          if result.abi_url != "":
            evm_logs <- FevmJsonRpc.eth_get_logs(result.node_url, result.abi_url, from_block_hex, "latest", result.address, [result.topic])
          else:
            evm_logs <- FevmJsonRpc.eth_get_logs(result.node_url, ERC1155_ABI_URL, from_block_hex, "latest", result.address, [result.topic])
        if result.token_type == "erc721":
          if result.abi_url != "":
            evm_logs <- FevmJsonRpc.eth_get_logs(result.node_url, result.abi_url, from_block_hex, "latest", result.address, [result.topic])
          else:
            evm_logs <- FevmJsonRpc.eth_get_logs(result.node_url, ERC721_ABI_URL, from_block_hex, "latest", result.address, [result.topic])

        for log <- evm_logs:
          datas: *EventLogParamResult

          n <- MyOp2.array_length(log)
          for llog <- log par:
            log_data = Node.deserialize_fork(llog.data)
            if log_data.data != "":
              if log_data.data != "00":
                forks = FevmJsonRpc.decode_abi(["string", "bytes"], log_data.data)

                -- parsing Minted event log (data_key/empty, data)
                if MyOp.array_length(forks) == 2:
                  metaContract <- Node.get_meta_contract_by_id(result.meta_contract_id)
                  dataKey <- Node.generate_data_key(result.chain, result.address, log_data.token_id)

                  MetaContract metaContract.meta.meta_contract_id
                  meta_result <- MetaContract.on_mint(metaContract.meta, dataKey, log_data.token_id, forks[1])

                  if meta_result.result:
                    Node.set_metadata_cron(metaContract.meta, result, log_data.token_id, meta_result.result, meta_result.metadatas)
                    Node.send_cron_tx(result.hash, llog.data, llog.block_number, llog.transaction_hash, log_data.token_id)
              datas <<- llog
              datas2 <<- llog
          join datas[n-1]
      join cron_datas[cron_length-1]

func get_cron_tx_latest_block() -> u64:
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    result <- Node.get_cron_tx_latest_block("0x0692f233614802D06e86CBDE6b5D090A9F5d3c10", "80001", "0x957f358702a4cd1c866fb29aefe080a9a56303adf02d47a081a56707d2d30033")
  <- result


func get_all_cron_txs() -> FdbCronTxsResult:
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    result <- Node.get_all_cron_txs()
  <- result

func search_metadatas(query: []MetadataQuery, ordering: []MetadataOrdering, from: u32, to:u32) -> FdbMetadatasResult:
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    result <- Node.search_metadatas(query, ordering, from, to)
  <- result   

func publish_batch(txs: []TransactionRequest) -> []FdbResult:
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    results <- Node.publish_batch(txs)
  <- results

func get_metadatas(data_key: string, version: string) -> FdbMetadatasResult:
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    result <- Node.get_metadatas(data_key, version)
  <- result

func get_meta_contract(token_key: string) -> FdbMetaContractResult:
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    result <- Node.get_meta_contract(token_key)
  <- result

func get_crons() -> FdbCronsResult:
  on HOST_PEER_ID:
    Node NODE_SERVICE_ID
    results <- Node.get_active_crons()
  <- results

func decode_event(data: string) -> []string:
  on HOST_PEER_ID:
    result <- FevmJsonRpc.decode_abi(["string", "bytes"], data)
  <- result